//@version=5
indicator("Daily Vertical Lines", overlay=true, max_lines_count=200)

// Храним время как 1400, 1530, 1730
times = array.from(1400, 1530, 1730, 2000, 2200)

getH(v) => math.floor(v / 100)
getM(v) => v % 100

tz = "Asia/Bangkok"

// --- ПРОШЛЫЕ ЛИНИИ
for i = 0 to array.size(times) - 1
    v = array.get(times, i)
    h = getH(v)
    m = getM(v)
    if barstate.isconfirmed and hour(time, tz) == h and minute(time, tz) == m
        line.new(bar_index, high, bar_index, low, xloc=xloc.bar_index, extend=extend.both, color=color.gray, style=line.style_dotted, width=1)

// --- линии НА СЕГОДНЯ ВПЕРЁД
var int  plannedDay  = na
var bool plannedDone = false
curDay = dayofmonth(time, tz)
if na(plannedDay) or curDay != plannedDay
    plannedDay  := curDay
    plannedDone := false

if barstate.islast and not plannedDone
    y  = year(time, tz)
    mo = month(time, tz)
    d  = curDay
    nowTs = time
    dayEnd = timestamp(tz, y, mo, d, 23, 59)
    for i = 0 to array.size(times) - 1
        v = array.get(times, i)
        h = getH(v)
        m = getM(v)
        tstamp = timestamp(tz, y, mo, d, h, m)
        if tstamp >= nowTs and tstamp <= dayEnd
            line.new(tstamp, high, tstamp, low, xloc=xloc.bar_time, extend=extend.both, color=color.gray, style=line.style_dotted, width=1)
    plannedDone := true
